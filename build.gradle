plugins {
  id 'fabric-loom' version '1.6-SNAPSHOT'
  id 'maven-publish'
}

version = project.mod_version
group = project.group_id

base {
  archivesName = project.mod_id
}

repositories {
  maven {
    url "https://maven.rnda.dev/releases/"
  }
  maven {
    url "https://maven.terraformersmc.com/releases/"
  }
}

dependencies {
  minecraft "com.mojang:minecraft:${minecraft_version}"
  mappings "net.fabricmc:yarn:${minecraft_version}+${yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${loader_version}"

  modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}+${minecraft_version}"
  modImplementation "com.terraformersmc:modmenu:${mod_menu_version}"
}

processResources {
  inputs.property "version", mod_version

  filesMatching("fabric.mod.json") {
    expand "version": mod_version
  }
}

tasks.withType(JavaCompile).configureEach {
  it.options.release = 21
}

java {
  withSourcesJar()

  sourceCompatibility = JavaVersion.VERSION_21
  targetCompatibility = JavaVersion.VERSION_21
}

jar {
  from("LICENSE") {
    rename {"${it}_${mod_id}"}
  }
}

publishing {
  publications {
    create("mavenJava", MavenPublication) {
      groupId = group_id
      artifactId = mod_id
      version = "${mod_version}+${minecraft_version}"
      from components.java
    }
  }

  repositories {
    maven {
      url "https://maven.rnda.dev/releases/"
      credentials.username selfHostedMavenUser
      credentials.password selfHostedMavenPass
    }
  }
}
